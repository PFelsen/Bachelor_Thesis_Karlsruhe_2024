# Macro to set an option for M++
MACRO(set_option OPT_NAME OPT_VALUE)
    if (NOT DEFINED ${OPT_NAME})
        set(${OPT_NAME} ${OPT_VALUE} CACHE STRING "OPT_NAME")
        message(STATUS "Option:     ${OPT_NAME}= $CACHE{${OPT_NAME}}")
    else ()
        message(STATUS "Option:     ${OPT_NAME}= $CACHE{${OPT_NAME}}   (using cached value)")
    endif()
    list(APPEND SET_OPTIONS "${OPT_NAME}")
ENDMACRO()

MACRO(set_sub_option OPT_NAME SUB_OPT_NAME SUB_OPT_VALUE)
    if (NOT DEFINED ${OPT_NAME})
        message(FATAL_ERROR "Require option " ${OPT_NAME} " for sub option " ${SUB_OPT_NAME})
    endif()

    if (${OPT_NAME})
        if (NOT DEFINED ${SUB_OPT_NAME})
            set(${SUB_OPT_NAME} ${SUB_OPT_VALUE} CACHE STRING "SUB_OPT_NAME")
            message(STATUS "Sub option: ${SUB_OPT_NAME}= $CACHE{${SUB_OPT_NAME}}")
        else ()
            message(STATUS "Sub option: ${SUB_OPT_NAME}= $CACHE{${SUB_OPT_NAME}}   (using cached value)")
        endif ()
    else ()
        set(${SUB_OPT_NAME} OFF CACHE STRING "SUB_OPT_NAME" FORCE)
        if (${SUB_OPT_VALUE})
            message(STATUS "Sub option: ${SUB_OPT_NAME}= $CACHE{${SUB_OPT_NAME}} (forced value; value was ${SUB_OPT_VALUE})")
        else ()
            message(STATUS "Sub option: ${SUB_OPT_NAME}= $CACHE{${SUB_OPT_NAME}}   (forced value)")
        endif ()
    endif ()
    list(APPEND SET_OPTIONS "${SUB_OPT_NAME}")
ENDMACRO()